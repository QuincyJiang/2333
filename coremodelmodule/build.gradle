/*
 * Created by QuincyJiang on 18-9-18 上午10:56
 * Copyright © 2018. All rights reserved.
 *
 * Last modified 18-9-18 上午10:56
 */
boolean isModuleMode = project.hasProperty("isModule") ? isModule.toBoolean() : false
if (isModuleMode) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion 27
    resourcePrefix 'core_model'

    useLibrary 'org.apache.http.legacy'
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }
    signingConfigs {
        releaseConfig {
            storeFile file("../kuaila2")
            storePassword project.hasProperty("KEYSTORE_PASS") ? KEYSTORE_PASS : System.getenv("KEYSTORE_PASS")
            keyAlias project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : System.getenv("ALIAS_NAME")
            keyPassword project.hasProperty("ALIAS_PASS") ? ALIAS_PASS : System.getenv("ALIAS_PASS")
        }
    }

    dataBinding {
        enabled = true
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }
    sourceSets {
        main {
            if (isModuleMode) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':commonservicelib')
    implementation project(':dependencylib')
    //router
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.1'
    configurations {
        all*.exclude group: 'com.google.code.gson'
        all*.exclude group: 'com.squareup.okhttp3'
        all*.exclude group: 'com.squareup.retrofit2'
        all*.exclude group: 'io.reactivex.rxjava2:rxjava'
        all*.exclude group: 'io.reactivex.rxjava2:rxandroid'
    }
    implementation 'cn.bmob.android:bmob-sdk:3.6.3'

    //bmob-push：Bmob的推送包
    implementation 'cn.bmob.android:bmob-push:0.8'

    //bmob-im：Bmob的即时通讯包，注意每个版本的im依赖特定版本的bmob-sdk，具体的依赖关系可查看下面注释[2]
    implementation 'cn.bmob.android:bmob-im:2.1.0@aar'

    //如果你想应用能够兼容Android6.0，请添加此依赖(org.apache.http.legacy.jar)
    implementation 'cn.bmob.android:http-legacy:1.0'
}
